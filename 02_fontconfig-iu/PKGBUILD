# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: bohoomil <bohoomil@zoho.com>

pkgname=fontconfig-infinality-ultimate
_basename=fontconfig
pkgver=2.13.1+12+g5f5ec56
pkgrel=1
epoch=2
pkgdesc="A library for configuring and customizing font access, optimized for freetype2-infinality-ultimate."
arch=(x86_64)
url="https://www.freedesktop.org/wiki/Software/fontconfig/"
license=(custom MIT)
changelog=CHANGELOG
groups=(infinality-bundle)
depends=(expat freetype2-infinality-ultimate)
makedepends=(git autoconf-archive gperf python-lxml python-six docbook-utils docbook-sgml
             perl-sgmls 'texlive-formatsextra>=2017' lynx json-c)
provides=('fontconfig=$pkgver' fontconfig-infinality fontconfig-infinality-ultimate)
conflicts=(fontconfig fontconfig-infinality fontconfig-infinality-git fontconfig-infinality-ultimate-git)
replaces=(fontconfig-iu-comp)
backup=(etc/fonts/fonts.conf
        etc/fonts/conf.avail.infinality/combi/30-metric-aliases-combi.conf
        etc/fonts/conf.avail.infinality/combi/37-repl-global-combi.conf
        etc/fonts/conf.avail.infinality/combi/60-latin-combi.conf
        etc/fonts/conf.avail.infinality/combi/65-non-latin-combi.conf
        etc/fonts/conf.avail.infinality/combi/66-aliases-wine-combi.conf
        etc/fonts/conf.avail.infinality/35-repl-custom.conf
        etc/fonts/conf.avail.infinality/38-repl-webfonts-custom.conf
        etc/fonts/conf.avail.infinality/97-selective-rendering-custom.conf)
options=('!libtool')
install=fontconfig-ultimate.install
_commit=5f5ec5676c61b9773026a9335c9b0dfa73a73353  # master
patches=(02-configure.ac.patch
         04-Makefile-20171231.conf.d.patch
         05-Makefile.am.patch
)

source=("git+https://anongit.freedesktop.org/git/fontconfig#commit=$_commit"
        fontconfig-infinality-conf.tar.bz2
        ${patches[@]}
        fontconfig.hook)
sha256sums=('SKIP'
            '092e1d5afb0118136544677cced0a10d6b759e74b38db1483bd3023d9be853d4'
            '9562b27eaac570ecebf0208f3304f78e3f2dd23ec635efd7469733e4ff548f06'
            '83f899675e97088010d8f8b5744ea953428999d39d2d7d8ccf2d589c3b1fedc8'
            'ac6411b71695d602522982797b58d2f24a964ba6cf66b8803e508eeebf0b6198'
            'aa309f0c91451ac14d7094859595cb4487992d5ca67dc04d6e99deff2670c3f8')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

	
#source=(http://www.fontconfig.org/release/${_basename}-${pkgver}.tar.bz2
#        ${_basename}-ultimate-git.tar.bz2
#        fc-cache-ib.hook)

# a nice page to test font matching:
# http://zipcon.net/~swhite/docs/computers/browsers/fonttest.html

prepare() {
  # make the rest of scripts portable between fontconfig & fontconfig-infinality
  [ -d "${pkgname}" ] || ln -sf fontconfig "${pkgname}"
  cp -r fontconfig-infinality-conf/conf.d.infinality "${pkgname}"/conf.d.infinality
  cd $pkgname
  for patch in "${patches[@]}"; do
    patch -Np1 -i ../${patch}
  done

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --with-templateinfdir=/etc/fonts/conf.avail.infinality \
    --with-xmldir=/etc/fonts \
    --localstatedir=/var \
    --disable-static \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/local/share/fonts
  make
}

check() {
  cd $pkgname
  make -k check
}

_install_conf() {
  echo "about to _install_conf($1)..."
  install -m644 "$1" "$pkgdir/etc/fonts/conf.avail"
  ln -s "../conf.avail/${1##*/}" "$pkgdir/etc/fonts/conf.d"


  cd "${srcdir}/${_basename}-ultimate-git"

  # copy presets
  cp -r "${srcdir}"/fontconfig-infinality-conf/conf.avail.infinality/{combi,free,ms,fonts-settings} \
    "${pkgdir}"/etc/fonts/conf.avail.infinality

}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 ../fontconfig.hook "$pkgdir/usr/share/libalpm/hooks/fontconfig.hook"

  #install infinality stuff
  install -m755 "${srcdir}"/fontconfig-infinality-conf/fc-presets \
    "${pkgdir}"/usr/bin/fc-presets

  # copy documentation
  install -m755 -d "${pkgdir}"/usr/share/doc/"${pkgname}"
  cp -r doc "${pkgdir}"/usr/share/

}

